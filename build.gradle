group 'uk.gov.digital.ho.proving.income'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.3")
        classpath("org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11")
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'application'
apply plugin: 'org.asciidoctor.convert'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = 'uk.gov.digital.ho.proving.income.api'
    version = '0.1.0'
}

repositories {
    mavenCentral()
    mavenLocal()    // Pick up the domain jar locally until we have a central repo server.
    maven { url 'https://repo.spring.io/libs-milestone' }
}

// API documentation tests are separated from unit tests and are not run automatically.
// Use the asciidoctor task to execute tests and generate HTML documentation (output to build/asciidoc)
// Use the apiDocTests task to execute tests (output to build/generated-snippets)
sourceSets {
    doc {
        java.srcDir file('src/doc/java')
        resources.srcDir file('src/doc/resources')
        compileClasspath += main.output + test.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web:1.3.3.RELEASE'
    compile 'org.springframework.boot:spring-boot:1.3.3.RELEASE'
    compile 'org.mongodb:mongodb-driver:3.0.4'
    compile 'org.mongodb:bson:3.0.4'
    compile 'org.json:json:20160212'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.2'
    compile 'org.codehaus.groovy:groovy-all:2.4.3'
    compile 'org.slf4j:slf4j-parent:1.7.14'
    compile 'ch.qos.logback:logback-core:1.1.3'
    compile 'ch.qos.logback:logback-classic:1.1.3'

    testCompile 'org.codehaus.groovy:groovy-all:2.4.3'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4'
    testCompile 'org.springframework:spring-test:4.2.5.RELEASE'
    testCompile 'cglib:cglib-nodep:3.2.1'
    testCompile 'org.objenesis:objenesis:2.2'

    testCompile 'com.jayway.jsonpath:json-path'
    testCompile 'org.springframework.restdocs:spring-restdocs-core:1.1.0.RC1'
    testCompile 'org.springframework.restdocs:spring-restdocs-restassured:1.1.0.RC1'
    testCompile 'org.mockito:mockito-all:1.10.19:'
}

task apiDocTests(type: Test) {
    description = 'Runs the API documentation tests'
    group = 'documentation'
    testClassesDir = sourceSets.doc.output.classesDir
    classpath = sourceSets.doc.runtimeClasspath
    reports.junitXml.destination = 'build/doc-test-results'
    reports.html.destination = 'build/reports/documentationTest'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

asciidoctor {
    backends    'pdf', 'html5'
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn apiDocTests
    sourceDir 'src/doc/asciidoc'
}

mainClassName = "uk.gov.digital.ho.proving.income.ServiceRunner"
